// --- Compiler Directives ---

#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
#ENABLE_TRACE

#DEFINE_CONSTANT MAX_NAME_LEN 50
#DEFINE_CONSTANT MAX_DIAL_LEN 100
#DEFINE_CONSTANT MAX_TEXT_LEN 255

#USER_SIMPLSHARP_LIBRARY "CiscoRoomKit"

// --- Inputs ---

DIGITAL_INPUT Connect;
DIGITAL_INPUT Debug;
DIGITAL_INPUT Dial;
DIGITAL_INPUT Hang_Up;
STRING_INPUT  Video_Number[MAX_DIAL_LEN];
STRING_INPUT  To_Device[MAX_TEXT_LEN];

// --- Outputs ---

DIGITAL_OUTPUT Connect_Fb;
DIGITAL_OUTPUT Error_Fb;
STRING_OUTPUT  Error_Message;
STRING_OUTPUT  Call_Status;
STRING_OUTPUT  From_Device;

// --- Parameters ---

STRING_PARAMETER Host[MAX_NAME_LEN];
STRING_PARAMETER User[MAX_NAME_LEN];
STRING_PARAMETER Password[MAX_NAME_LEN];

// --- Global Variables ---

Codec RoomKit;

// --- Events ---

PUSH Connect
{
	Try
	{
		RoomKit.Connect();

		Error_Fb = 0;
		Error_Message = "";
	}
	Catch
	{
		Error_Fb = 1;
		Error_Message = GetExceptionMessage();
	}
}

RELEASE Connect
{
	RoomKit.Disconnect();
}

PUSH Dial
{
	RoomKit.Dial();
}

PUSH Hang_Up
{
	RoomKit.HangUp();
}

CHANGE Video_Number
{
	RoomKit.VideoNumber = Video_Number;
}

CHANGE To_Device
{
	Try
	{
		RoomKit.SendCommand(To_Device);
	}
	Catch
	{
		Error_Fb = 1;
		Error_Message = GetExceptionMessage();
	}
}

EVENTHANDLER Codec_OnConnect (Codec sender, EventArgs args)
{
	Connect_Fb = 1;
}

EVENTHANDLER Codec_OnDisconnect (Codec sender, EventArgs args)
{
	Connect_Fb = 0;
}

EVENTHANDLER Codec_OnResponse (Codec sender, DataEventArgs args)
{
	If (Debug)
	{
		From_Device = args.Message;
	}
}

EVENTHANDLER Codec_OnCallStatus (Codec sender, DataEventArgs args)
{
	Call_Status = args.Message;
}

// --- Main ---

Function Main()
{
	RoomKit.Host = Host;
	RoomKit.User = User;
	RoomKit.Password = Password;

	RegisterEvent(RoomKit, OnConnect, Codec_OnConnect);
	RegisterEvent(RoomKit, OnDisconnect, Codec_OnDisconnect);
	RegisterEvent(RoomKit, OnResponse, Codec_OnResponse);
	RegisterEvent(RoomKit, OnCallStatus, Codec_OnCallStatus);

	WaitForInitializationComplete();
}
