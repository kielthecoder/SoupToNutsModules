// --- Compiler Directives ---

// #CATEGORY "" 
// #DIGITAL_EXPAND 
// #ANALOG_SERIAL_EXPAND 

#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
// #ENABLE_TRACE

#DEFINE_CONSTANT MAX_PAGE_SIZE 10

#USER_SIMPLSHARP_LIBRARY "SoupToNuts"

// --- Inputs ---

DIGITAL_INPUT Save;

STRING_INPUT  New_Contact_Name[100];
STRING_INPUT  New_Contact_Number[100];
DIGITAL_INPUT Add_New_Contact;

ANALOG_INPUT  Select;
ANALOG_INPUT  Select_Page_Entry;
DIGITAL_INPUT Remove_Selected;

ANALOG_INPUT  Current_Page;

// --- Outputs ---

DIGITAL_OUTPUT Initialized_Fb;
DIGITAL_OUTPUT Save_Fb;

ANALOG_OUTPUT Select_Fb;
STRING_OUTPUT Selected_Entry_Name;
STRING_OUTPUT Selected_Entry_Number;

ANALOG_OUTPUT Current_Page_Fb;
ANALOG_OUTPUT Total_Pages_Fb;

STRING_OUTPUT Page_Entry_Name[MAX_PAGE_SIZE];

// --- Module Parameters ---

STRING_PARAMETER File_Name[200];

// --- Global Variables ---

Phonebook Contacts;

// --- Events ---

THREADSAFE PUSH Save
{
	Contacts.Save();
}

THREADSAFE PUSH Add_New_Contact
{
	Contacts.Add(New_Contact_Name, New_Contact_Number);
}

THREADSAFE CHANGE Select
{
	Contacts.Selection = Select;
}

THREADSAFE CHANGE Select_Page_Entry
{
	Contacts.SelectPageEntry = Select_Page_Entry;
}

THREADSAFE PUSH Remove_Selected
{
	If (Contacts.Selection > 0)
		Contacts.Remove(Contacts.Selection);
}

THREADSAFE CHANGE Current_Page
{
	Contacts.CurrentPage = Current_Page;
}

// --- Callbacks ---

CALLBACK FUNCTION Contacts_OnInitialize (INTEGER success)
{
	Contacts.PageSize = MAX_PAGE_SIZE;
	Contacts.CurrentPage = 1;

	Total_Pages_Fb = Contacts.TotalPages;
	Initialized_Fb = success;
}

CALLBACK FUNCTION Contacts_OnSave (INTEGER success)
{
	Save_Fb = success;
}

CALLBACK FUNCTION Contacts_OnSelection (INTEGER value)
{
	Select_Fb = value;
	Selected_Entry_Name = Contacts.SelectedEntryName;
	Selected_Entry_Number = Contacts.SelectedEntryNumber;
}

EVENTHANDLER Contacts_Updated (Phonebook sender, PhonebookUpdateEventArgs args)
{
	Trace("Contact %d updated: %s [ %s ]", args.Index, args.Name, args.Number);

	Total_Pages_Fb = Contacts.TotalPages;
	Contacts.CurrentPage = 1;
}

EVENTHANDLER Contacts_Page_Updated (Phonebook sender, PhonebookPageEventArgs args)
{
	INTEGER i;

	Current_Page_Fb = args.Page;

	For (i = 1 To sender.PageSize)
	{
		Page_Entry_Name[i] = args.Names[i - 1];
	}
}

// --- Main ---

Function Main()
{
	RegisterDelegate(Contacts, OnInitialize, Contacts_OnInitialize);
	RegisterDelegate(Contacts, OnSave, Contacts_OnSave);
	RegisterDelegate(Contacts, OnSelection, Contacts_OnSelection);

	RegisterEvent(Contacts, PhonebookUpdated, Contacts_Updated);
	RegisterEvent(Contacts, PageUpdated, Contacts_Page_Updated);

	WaitForInitializationComplete();
	Contacts.Initialize(File_Name);
}

